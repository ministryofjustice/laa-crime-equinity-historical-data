plugins {
	id 'jacoco'
	id 'org.sonarqube' version '4.0.0.2929'
	id "io.sentry.jvm.gradle" version "4.10.0"
}

def versions = [
		springdocVersion			: '1.8.0',
		springStarter				: '3.3.2',
		springWeb					: '6.2.4',
		springSecurity				: '6.3.0',
		logback						: '1.5.6',
		mapStruct					: '1.5.5.Final',
		openAPI						: '2.6.0',
		sentryVersion				: '7.13.0',
		micrometerio 				: '1.3.2',
		prometheus 					: '1.12.6',
]

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

    configureEach {
		resolutionStrategy {
			// List below contains dependencies included as part of a bundled package
			// with known vulnerabilities, and the version to use as a fix
			// This will still need to be included manually under the dependencies section
			// This will only ensure the override of the bundled version
			force 'org.apache.tomcat.embed:tomcat-embed-core:10.1.25'
			force 'io.netty:netty-codec-http:4.1.108.Final'
			force "org.springframework:spring-web:$versions.springWeb"
		}
	}
}


dependencyManagement {
	dependencies {
		dependencySet(group: 'io.sentry', version: versions.sentryVersion) {
			entry 'sentry-spring-boot-starter'
			entry 'sentry-logback'
		}
	}
}

dependencies {
	implementation project(':data-api')

	// Spring
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:$versions.springStarter"
	implementation "org.springframework.boot:spring-boot-starter-web:$versions.springStarter"
	implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.25'

	// Logs
	implementation "ch.qos.logback:logback-core:$versions.logback"
	implementation "ch.qos.logback:logback-classic:$versions.logback"


	//Enable Swagger UI
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$versions.openAPI"

	// Security (oAuth)
	implementation "org.springframework.security:spring-security-core:$versions.springSecurity"
	implementation "org.springframework.security:spring-security-web:$versions.springSecurity"
	implementation "org.springframework.security:spring-security-config:$versions.springSecurity"
	implementation "org.springframework.boot:spring-boot-starter-security:$versions.springStarter"

	implementation "org.springframework.boot:spring-boot-starter-webflux:$versions.springStarter"
	implementation 'io.netty:netty-codec-http:4.1.108.Final'

	implementation "org.springframework.boot:spring-boot-starter-actuator:$versions.springStarter"
	implementation "org.springframework.boot:spring-boot-starter-validation:$versions.springStarter"

	// Sentry
	implementation platform("io.sentry:sentry-bom:$versions.sentryVersion")
	implementation "io.sentry:sentry-logback:$versions.sentryVersion"
	implementation "io.sentry:sentry-spring-boot-starter-jakarta:$versions.sentryVersion"

	// Metrics
	implementation "io.micrometer:micrometer-tracing-bom:$versions.micrometerio"
	implementation "io.micrometer:micrometer-tracing-bridge-brave:$versions.micrometerio"
	implementation "io.micrometer:micrometer-registry-prometheus:$versions.prometheus"


	// Annotations
	implementation "org.springdoc:springdoc-openapi-ui:$versions.springdocVersion"
	implementation 'com.microsoft.sqlserver:mssql-jdbc'
	implementation 'org.json:json:20240303'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation "org.mapstruct:mapstruct:$versions.mapStruct"
	annotationProcessor "org.mapstruct:mapstruct-processor:$versions.mapStruct"

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$versions.springStarter"

	// If using JUnit Jupiter
	testImplementation('org.junit.jupiter:junit-jupiter:5.11.0-M1')
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")

	testImplementation "org.springframework.boot:spring-boot-starter-test:$versions.springStarter"
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "com.h2database:h2:2.2.224"
}

jar {
	manifest {
		attributes(
			'Main-Class': 'uk.gov.justice.laa.crime.equinity.historicaldata.EquinityHistoricalDataApplication'
		)
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.8"
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/entity/**',
					'**/dto/**',
					'**/generated/**',
					'**/config/**'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		failOnViolation = true
		rule {
			element = 'CLASS'
			excludes = [
				'uk.gov.justice.laa.crime.equinity.historicaldata.EquinityHistoricalDataApplication',
				'uk.gov.justice.laa.crime.equinity.historicaldata.config.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.exception.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.mapper.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.model.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.security.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.archive.config.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.archive.mapper.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.archive.model.*',
				'uk.gov.justice.laa.crime.equinity.historicaldata.controller.CrmFileController' // This is a temporary test class
			]
			limit {
				minimum = 0.70
			}
		}
	}
}

sonar {
	properties {
		property "sonar.projectName", "laa-crime-equinity-historical-data"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.organization", "ministryofjustice"
		property "sonar.projectKey", "ministryofjustice_laa-crime-equinity-historical-data"

		property "sonar.exclusions", "**/entity/**.java , **/model/**.java , **/dto/**.java , **/config/**.java, **/jms/**.java,**/exception/**.java,**/handler/**.java,**/maatapi/**Client**.java,**/generated/**"
		property "sonar.coverage.exclusions", "**/EquinityHistoricalDataApplication.java"
		property "sonar.coverage.jacoco.xmlReportPaths",
				"${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
	}
}
