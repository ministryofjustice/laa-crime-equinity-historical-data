plugins {
    id 'org.openapi.generator' version '7.0.1'
}

apply plugin: "maven-publish"

def versions = [
        springStarter               : '3.3.7',
        springWeb                   : '6.1.14',
        springData                  : '3.3.4',
        springdoc                   : '1.8.0',
        tomcatEmbed                 : '10.1.35',
        jacksonFaster               : '2.17.2',
        jacksonNullable             : '0.2.6',
        googleFirebugs              : '3.0.2'
]

dependencies {
    implementation "org.springframework:spring-webmvc:$versions.springWeb"
    implementation "org.springframework:spring-web:$versions.springWeb"
    implementation "org.springframework.boot:spring-boot-starter-web:$versions.springStarter"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:$versions.tomcatEmbed"

    implementation "org.springframework:spring-webflux:$versions.springWeb"

    implementation "org.springframework.data:spring-data-commons:$versions.springData"
    implementation "org.springdoc:springdoc-openapi-ui:$versions.springdoc"

    implementation "com.google.code.findbugs:jsr305:$versions.googleFirebugs"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$versions.jacksonFaster"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$versions.jacksonFaster"
    implementation "org.openapitools:jackson-databind-nullable:$versions.jacksonNullable"
    implementation "org.springframework.boot:spring-boot-starter-validation:$versions.springStarter"
    implementation "com.fasterxml.jackson.core:jackson-databind:$versions.jacksonFaster"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url 'https://maven.pkg.github.com/ministryofjustice/laa-crime-equinity-historical-data'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

sourceSets.main.java.srcDirs += ["$rootDir/data-api/"]

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/data-api/open-api-specification.yml".toString() // Change this to the location of your OpenAPI specification
    outputDir = "$rootDir/data-api".toString()
    apiPackage = "uk.gov.justice.laa.crime.equinity.historicaldata.generated.api"
    modelPackage = "uk.gov.justice.laa.crime.equinity.historicaldata.generated.dto"
    invokerPackage = "uk.gov.justice.laa.crime.equinity.historicaldata.generated.invoker"
    configOptions = [
        delegatePattern       : "false",
        interfaceOnly         : "true", // This will only generate interfaces, not implementations
        dateLibrary           : "java17",
        java17                : "true",
        useTags               : "true",
        skipDefaultInterface  : "true",
        useJakartaEe          : "true",
        documentationProvider : "none",
        serializableModel     : "true"
    ]
}

compileJava.dependsOn 'openApiGenerate'

clean {
    delete "$rootDir/data-api/src"
}

// disable for overall project when running a build
bootJar {
    enabled = false
}

bootRun {
    enabled = false
}
